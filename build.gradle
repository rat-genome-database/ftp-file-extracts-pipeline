// create a runnable jar with jar dependencies stored in lib subdirectory
tasks.whenTaskAdded { task ->
    ['startScripts', 'distTar'].each { String skipTaskName ->
        if (task.name.contains(skipTaskName)) {
            task.enabled = false
        }
    }
}
apply plugin: 'java'
apply plugin: 'application'

// JDK 8
sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'edu.mcw.rgd.FtpFileExtractsManager'
String myAppName = 'ftpFileExtracts'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.apache.commons:commons-dbcp2:2.8.0'
    compile 'commons-net:commons-net:3.6'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.12.4'
    compile 'log4j:log4j:1.2.17'
    compile 'com.oracle.ojdbc:ojdbc8:19.3.0.0'
    compile 'org.springframework:spring-beans:5.3.9'
    compile 'org.springframework:spring-jdbc:5.3.9'

    compile fileTree(dir: 'lib', include: '*.jar')
}

jar {
    baseName = myAppName
    manifest {
        attributes(
            'Built-By': System.getProperty('user.name'),
            'Built-Date': new Date(),
            'Built-JDK': System.getProperty('java.version'),
            'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
            'Main-Class': mainClassName
        )
    }
}

distributions {
    main {
        baseName = myAppName
    }
}

task createDistro(type: Copy) {
    def zipFile = file('build/distributions/'+myAppName+'.zip')
    def outputDir = file("build/install")

    from zipTree(zipFile)
    into outputDir
}
createDistro.dependsOn assembleDist
